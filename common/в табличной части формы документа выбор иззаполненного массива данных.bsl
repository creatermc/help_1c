


// в табличной части формы документа выбор иззаполненного массива данных


&НаКлиенте
Процедура ТоварыСписатьНаРасходыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	УстановитьПараметрыВыбораСтатьиРасходов(
		Элементы.ТоварыСтатьяРасходов.ПараметрыВыбора, ТекущиеДанные);

КонецПроцедуры


// Устанавливает параметры выбора статьи расходов в зависимости от типа номенклатуры.
//
// Параметры:
//  ПараметрыВыбора - Структура             - параметры выбора строки.
//  ТекущиеДанные   - ДанныеФормыЭлементКоллекции - текущая строка.
//
Процедура УстановитьПараметрыВыбораСтатьиРасходов(ПараметрыВыбора, ТекущиеДанные) Экспорт
	
	Если ТекущиеДанные = Неопределено
		ИЛИ НЕ ТекущиеДанные.СписатьНаРасходы 
		Или Не ЗначениеЗаполнено(ТекущиеДанные.ТипНоменклатуры) Тогда
		Возврат;
	КонецЕсли; 
	
	НовыеПараметрыВыбора = Новый Массив;
	
	Если ТекущиеДанные.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.Товар")
		ИЛИ ТекущиеДанные.ТипНоменклатуры = ПредопределенноеЗначение("Перечисление.ТипыНоменклатуры.МногооборотнаяТара")
		Тогда
			
		КлючПараметраВыбора  = "Отбор.ВариантРаспределенияРасходов";
		ДопустимыеВариантыРаспределения = ВариантыРаспределенияРасходовДляСписанияТоваровПриПоступлении();
		
		НовыеПараметрыВыбора.Добавить(Новый ПараметрВыбора(КлючПараметраВыбора, ДопустимыеВариантыРаспределения));
	
	КонецЕсли;
	
	ПараметрыВыбора = Новый ФиксированныйМассив(НовыеПараметрыВыбора);
	
КонецПроцедуры




// Возвращает список допустимых вариантов распределения расходов для списания товаров на расходы при поступлении.
// 
// Возвращаемое значение:
//	Массив из ПеречислениеСсылка.ВариантыРаспределенияРасходов - список допустимых вариантов распределения.
//
Функция ВариантыРаспределенияРасходовДляСписанияТоваровПриПоступлении()
	
	ВариантыРаспределения = Новый Массив; // Массив из ПеречислениеСсылка.ВариантыРаспределенияРасходов
	
	ВариантыРаспределения.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаПроизводственныеЗатраты"));
	ВариантыРаспределения.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПроизводства"));
	ВариантыРаспределения.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаСебестоимостьПродаж"));
	ВариантыРаспределения.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаНаправленияДеятельности"));
	ВариантыРаспределения.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НаРасходыБудущихПериодов"));
	ВариантыРаспределения.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРаспределенияРасходов.НеРаспределять"));
	
	Возврат ВариантыРаспределения;
	
КонецФункции
